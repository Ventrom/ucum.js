{
  helpers = require('../lib/helpers');
  prefixes = require('./prefixes.json');
  prefixMetadata = require('./prefixMetadata.json');
  metrics = require('./metrics.json');
  multiply = helpers.multiply;
  topower = helpers.topower;
  cleanup = helpers.cleanup;
  ismetric = helpers.ismetric(metrics);
}

start = e:ucumExpr {
  return e ; // cleanup(e);
}

ucumExpr = "/" e:expr {return multiply({value:1, units:{}}, [["/", e]]);} / expr

multiply  = "." term /  "/" term

expr =  t:term ms:(multiply)* {
  return multiply(t, ms);
}

term = e:element exp:(exponent)? (ANN)* !{return e.ann && exp;} {
  return topower(e, exp);
} / d:DIGITS  (ANN)*{
 var ret = {
    value: d,
    units: {}
  };
  return ret;
}

element  =   u:simpleUnit {return u;} /  "(" e:expr ")" {return e;} / ANN

exponent =  s:([+-])? d:DIGITS {return (s=="-") ? (-1*d) : d}

simpleUnit =  p:PREFIX? a:METRICATOM !{return(p && !ismetric(a));} {
  var ret = a;
  if (p){
    ret.value = ret.value * prefixes[p];
    ret.metadata = {};
    var u = Object.keys(ret.units)[0];
    if(prefixMetadata[p]){
      // if this prefix has metadata, augment the return with it
      Object.keys(prefixMetadata[p]).forEach(function(key){
        if(!ret.metadata[u]){
          ret.metadata[u] = { prefix: {} };
        }
        ret.metadata[u].prefix[key] = prefixMetadata[p][key];
      });
    }
  }
  return ret;
} / METRICATOM 

DIGITS = v:[0-9]+ epresent:'e'? e:exponent? !{return (!epresent && !!e);}  {
  return parseInt(v.join(""))*Math.pow(10, e||0);
} 

ANN = "{" m:[^}]+ "}" !{return /[^\x00-\x7F]/.test(m);} { return {value: 1, units:{}, ann: m} }

METRICATOM = {{METRICATOM}}
PREFIX = {{PREFIX}}
